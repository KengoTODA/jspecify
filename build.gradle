plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '5.1.0'
    id 'net.ltgt.errorprone' version '1.2.1'
    id 'org.javamodularity.moduleplugin' version '1.7.0'
}

ext {
    junitVersion = '5.7.0'
    errorproneVersion = '2.3.4'
}

repositories { mavenCentral() }

apply from: "gradle/mrjar.gradle"
apply from: "gradle/integration-test.gradle"
apply from: "gradle/format.gradle"
apply from: "gradle/publish.gradle"

dependencies {
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
}

java {
    withJavadocJar()
    // TODO(cpovirk): Use withSourcesJar if I can find a way to fully *override* its default value for `from`.

    task('sourcesJar', type: Jar, dependsOn: classes) {
        classifier = 'sources'
        exclude('**.class')
        // Prefer the copy of DefaultNonNull whose @Target includes MODULE.
        // TODO(cpovirk): Move configuration to mrjar.gradle? Maybe we could achieve it with task.named('sourcesJar') if mrjar.gradle were applied after we create this task?
        from sourceSets.java9.allJava + sourceSets.main_except_overlapping.allJava
    }
    tasks.named('assemble') {
        dependsOn('sourcesJar')
    }
}

defaultTasks 'spotlessApply', 'build'
